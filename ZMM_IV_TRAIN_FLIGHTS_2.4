@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Dimension Flights'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #TRANSACTIONAL
}

@VDM.viewType: #BASIC
@Analytics.dataCategory: #FACT


define view entity ZMM_IV_TRAIN_FLIGHTS 
    as select from sflight      as flight

    association [0..1] to I_CalendarDate    as  _CalendarDate   on  _CalendarDate.CalendarDate  =   $projection.Fldate

{
        // Keys
    key carrid      as Carrid,
    key connid      as Connid,
        @ObjectModel.foreignKey.association: '_CalendarDate'
    key fldate      as Fldate,
        
        // Characteristics        
        planetype   as Planetype,        
        
        // Association Dates
         @EndUserText.label: 'Flight Year'
        _CalendarDate.CalendarYear,
        @EndUserText.label: 'Flight Month'
        _CalendarDate.CalendarMonth,
                       
        // Measures
        @Semantics.amount.currencyCode: 'CurrencyCode'
        @Aggregation.default: #SUM
        price       as Price,
        @Semantics.amount.currencyCode: 'CurrencyCode'
        @Aggregation.default: #SUM
        paymentsum  as Paymentsum,
        @Aggregation.default: #SUM
        seatsmax    as MaximumCapacityEconomyClass,
        @Aggregation.default: #SUM
        seatsocc    as OccupiedEconomyClass,
        @Aggregation.default: #SUM
        seatsmax_b  as MaximumCapacityBusinessClass,
        @Aggregation.default: #SUM
        seatsocc_b  as OccupiedBusinessClass,
        @Aggregation.default: #SUM
        seatsmax_f  as MaximumCapacityFirstClass,
        @Aggregation.default: #SUM
        seatsocc_f  as OccupiedFirstClass,
        
        // Currency Code
        currency    as CurrencyCode,
        
        // Associations
        _CalendarDate
    
}
