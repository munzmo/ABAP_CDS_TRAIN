@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Dimension Customer'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MASTER
}

@VDM.viewType: #BASIC
@Analytics.dataCategory: #DIMENSION

define view entity ZMM_IV_CDS_TRAIN_CUST 
    as select from scustom
    
    association [0..1] to I_Country     as  _CountryCustomer        on  _CountryCustomer.Country    = $projection.CustomerCountry  
{
    
        @ObjectModel.text.element: [ 'CustomerName' ]
    key cast( id as s_customer preserving type )        as Customer,
    
        @Semantics.text: true
        name        as CustomerName,
        
        @ObjectModel.foreignKey.association: '_CountryCustomer'
        @Semantics.address.country: true
        country     as CustomerCountry,
        
        @Semantics.address.city: true
        city        as City,
        
        
        // Associations
        _CountryCustomer
    
}
