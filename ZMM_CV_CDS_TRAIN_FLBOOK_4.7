@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Analytical Query Flight Booking'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #XL,
    dataClass: #MIXED
}

@VDM.viewType: #CONSUMPTION
@Analytics.query: true


define view entity ZMM_CV_CDS_TRAIN_FLBOOK
    with parameters
        P_FlightDate    : s_date 
    as select from ZMM_RV_CDS_TRAIN_FLBOOK( P_FlightDate : $parameters.P_FlightDate )
{
    
        // Keys
        @AnalyticsDetails.query.axis: #ROWS
        @Consumption.filter: {  selectionType: #SINGLE,
                                multipleSelections: true,
                                mandatory: false}
    key AirlineCode,
    key FlightConnection,
    key FlightDate,
    key BookingNumber,
        
        TravelAgency,
        Customer,
        CustomerCountry,
        @EndUserText.label: 'Status Capacity Economy Class'
        StatusCapacity,
        
        
        
        // Measures
        @EndUserText.label: 'Free Capacity Economy Class'
        @AnalyticsDetails.query.hidden: true
        FreeCapacityEconomyClass,
        @EndUserText.label: 'AVG Capacity Business Class'
        @AnalyticsDetails.query.hidden: true
        AVGSeatsBusiness,
        @Semantics.quantity.unitOfMeasure: 'UnitWeight'
        @AnalyticsDetails.query.hidden: true
        @AnalyticsDetails.query.decimals: 3
        LuggageWeight,
        @Semantics.amount.currencyCode: 'PaymentCuky'
        @AnalyticsDetails.query.hidden: true
        @AnalyticsDetails.query.decimals: 2
        BookingPrice,
        
        @AnalyticsDetails.query.hidden: true
        @EndUserText.label: 'AVG Weight per Airline and Connection'        
        @AnalyticsDetails.query.decimals: 3
        @AnalyticsDetails: {
            exceptionAggregationSteps: [
                {   exceptionAggregationBehavior: #AVG,
                    exceptionAggregationElements: ['AirlineCode', 'FlightConnection']  }
                    ] }        
        @AnalyticsDetails.query.formula: '(LuggageWeight)' 0
        as AVGLuggageWeightPerAirline,
        
        @EndUserText.label: 'Booking Price w/o Taxes'
        @AnalyticsDetails.query.hidden: true
        @AnalyticsDetails.query.decimals: 2
        @AnalyticsDetails.query:{ formula: 'BookingPrice * 0.81' }
        1 as BookingPriceTaxes,
        
        // Units
        UnitWeight,
        PaymentCuky,
        
        // Associations
        _Airline,
        _Connection,
        _Customer,
        _FlightDate,
        _TrvAgency
}
